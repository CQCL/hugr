WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT    = _{ ";" ~ (!("\n") ~ ANY)* ~ "\n" }
identifier = @{ (ASCII_ALPHA | "_" | "-") ~ (ASCII_ALPHANUMERIC | "_" | "-")* }
ext_name   = @{ identifier ~ ("." ~ identifier)* }
symbol     = @{ identifier ~ ("." ~ identifier)+ }
tag        = @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) | "0" }

string         =  { "\"" ~ (string_raw | string_escape | string_unicode)* ~ "\"" }
string_raw     = @{ (!("\\" | "\"") ~ ANY)+ }
string_escape  = @{ "\\" ~ ("\"" | "\\" | "n" | "r" | "t") }
string_unicode = @{ "\\u" ~ "{" ~ ASCII_HEX_DIGIT+ ~ "}" }

base64_string = { "\"" ~ (ASCII_ALPHANUMERIC | "+" | "/")* ~ "="* ~ "\"" }

module = { "(" ~ "hugr" ~ "0" ~ ")" ~ meta* ~ node* ~ EOI }

meta = { "(" ~ "meta" ~ term ~ ")" }

edge_name  = @{ "%" ~ (ASCII_ALPHANUMERIC | "_" | "-")* }
port       =  { edge_name }
port_list  =  { "[" ~ port* ~ "]" }
port_lists = _{ port_list ~ port_list }

node = {
    node_dfg
  | node_cfg
  | node_block
  | node_define_func
  | node_declare_func
  | node_define_alias
  | node_declare_alias
  | node_declare_ctr
  | node_declare_operation
  | node_tail_loop
  | node_cond
  | node_tag
  | node_import
  | node_custom
}

node_dfg               = { "(" ~ "dfg" ~ port_lists? ~ signature? ~ meta* ~ region* ~ ")" }
node_cfg               = { "(" ~ "cfg" ~ port_lists? ~ signature? ~ meta* ~ region* ~ ")" }
node_block             = { "(" ~ "block" ~ port_lists? ~ signature? ~ meta* ~ region* ~ ")" }
node_define_func       = { "(" ~ "define-func" ~ func_header ~ meta* ~ region* ~ ")" }
node_declare_func      = { "(" ~ "declare-func" ~ func_header ~ meta* ~ ")" }
node_define_alias      = { "(" ~ "define-alias" ~ alias_header ~ term ~ meta* ~ ")" }
node_declare_alias     = { "(" ~ "declare-alias" ~ alias_header ~ meta* ~ ")" }
node_declare_ctr       = { "(" ~ "declare-ctr" ~ ctr_header ~ meta* ~ ")" }
node_declare_operation = { "(" ~ "declare-operation" ~ operation_header ~ meta* ~ ")" }
node_tail_loop         = { "(" ~ "tail-loop" ~ port_lists? ~ signature? ~ meta* ~ region* ~ ")" }
node_cond              = { "(" ~ "cond" ~ port_lists? ~ signature? ~ meta* ~ region* ~ ")" }
node_tag               = { "(" ~ "tag" ~ tag ~ port_lists? ~ signature? ~ meta* ~ region* ~ ")" }
node_import            = { "(" ~ "import" ~ symbol ~ meta* ~ ")" }
node_custom            = { "(" ~ term_apply ~ port_lists? ~ signature? ~ meta* ~ region* ~ ")" }

signature        = { "(" ~ "signature" ~ term ~ ")" }
func_header      = { symbol ~ param* ~ where_clause* ~ term }
alias_header     = { symbol ~ param* ~ term }
ctr_header       = { symbol ~ param* ~ where_clause* ~ term }
operation_header = { symbol ~ param* ~ where_clause* ~ term }

param        = { "(" ~ "param" ~ term_var ~ term ~ ")" }
where_clause = { "(" ~ "where" ~ term ~ ")" }

region     = { region_dfg | region_cfg }
region_dfg = { "(" ~ "dfg" ~ port_lists? ~ signature? ~ meta* ~ node* ~ ")" }
region_cfg = { "(" ~ "cfg" ~ port_lists? ~ signature? ~ meta* ~ node* ~ ")" }

term = {
    term_wildcard
  | term_var
  | term_const
  | term_list
  | term_list_type
  | term_str
  | term_float
  | term_nat
  | term_ext_set
  | term_apply
  | term_const_func
  | term_const_adt
  | term_bytes
  | term_meta
  | term_float
  | term_tuple
}

term_wildcard   =  { "_" }
term_var        =  { "?" ~ identifier }
term_apply      =  { symbol | ("(" ~ symbol ~ term* ~ ")") }
term_const      =  { "(" ~ "const" ~ term ~ term ~ ")" }
term_list       =  { "[" ~ (spliced_term | term)* ~ "]" }
term_tuple      =  { "(" ~ "tuple" ~ (spliced_term | term)* ~ ")" }
term_list_type  =  { "(" ~ "list" ~ term ~ ")" }
term_str        =  { string }
term_nat        = @{ (ASCII_DIGIT)+ }
term_ext_set    =  { "(" ~ "ext" ~ (spliced_term | ext_name)* ~ ")" }
term_const_func =  { "(" ~ "fn" ~ term ~ ")" }
term_const_adt  =  { "(" ~ "tag" ~ tag ~ term* ~ ")" }
term_bytes      =  { "(" ~ "bytes" ~ base64_string ~ ")" }
term_meta       =  { "meta" }
term_float      = @{ ("+" | "-")? ~ (ASCII_DIGIT)+ ~ "." ~ (ASCII_DIGIT)+ }

spliced_term = { term ~ "..." }
